
/* crossmod compatibility with Lava's Skie - Cost of one Soul Mod

"L#2SDA1" itm

Note: some of the following triggers have to be combined with others to make them distinct.

Global("L#2SkieDagger","GLOBAL",0) - party did not take soul dagger in ID

GlobalGT("L#2SkieDagger","GLOBAL",9) - soul dagger is with Nem

~GlobalLT("L#2SkieDagger","GLOBAL",11)~ - PC knows not yet that it takes another soul
GlobalGT("L#2SkieDagger","GLOBAL",10) -  PC knows that it takes another soul to retore it

GlobalLT("L#2SkieDagger","GLOBAL",15) -  PC didn't find a willing soul yet
GlobalGT("L#2SkieDagger","GLOBAL",14) -  PC found a willing soul

~GlobalGT("L#2SkieDagger","GLOBAL",30) Global("L#2SDFailedLater","GLOBAL",0)~ - Skies soul is restored

~GlobalGT("L#2SkieDagger","GLOBAL",32) Global("L#2SDFailedLater","GLOBAL",0)~ - Skie lives

Global("L#2SDToldImoenNo","GLOBAL",1) - quest broke in ID (no dagger)

~Global("L#2SDBreakRule","GLOBAL",1)~ //PC broke rule, dagger not restored, Skie cannot be saved

Global("L#2SDFailedLater","GLOBAL",1)  //quest broke after bringing the dagger: either by sending it back to BG or breaking the rule in the ritual

*/

/* PC tried to cheat and lost Skie's soul */

IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
	GlobalLT("C#Ajantis_CM_SkieCost","GLOBAL",12)
	Global("L#2SDBreakRule","GLOBAL",1) //PC did not lose Skie's soul
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",12)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",12)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END


/* PC found dagger in ID, independent on quest status but not lost Skie soul */
IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",0)
	OR(2)
		GlobalGT("C#AjantisFriendTalk","GLOBAL",2)
		GlobalGT("C#AjantisLoveTalk","GLOBAL",4)
	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
	GlobalGT("L#2SkieDagger","GLOBAL",0) //Skie Cost Mod started
	Global("L#2SDToldImoenNo","GLOBAL",0) //Player did not leave dagger in ID
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul
	OR(2)
		Global("L#2SDFailedLater","GLOBAL",0)  //quest is still running
		GlobalGT("L#2SDSendIt","GLOBAL",0) //Dagger was already sent back
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",1)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END



/* dagger was restored without Ajantis in the party - first comment about dagger did run */

/* check whether Ajantis was in party upon Skie's restoration */
IF
	!InParty(Myself)
	GlobalLT("C#Ajantis_CM_SkieCost","GLOBAL",6)
	GlobalGT("L#2SkieDagger","GLOBAL",30) //Skie's soul restored
	Global("L#2SDSendIt","GLOBAL",0) //dagger was not sent to BG
	Global("L#2SDToldImoenNo","GLOBAL",0) //Player did not leave dagger in ID
	Global("L#2SDFailedLater","GLOBAL",0)  //quest is still running  
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",6)
END

IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",6)
	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
	GlobalGT("L#2SkieDagger","GLOBAL",30) //Skie's soul restored
	Global("L#2SDSendIt","GLOBAL",0) //dagger was not sent to BG
	Global("L#2SDToldImoenNo","GLOBAL",0) //Player did not leave dagger in ID
	Global("L#2SDFailedLater","GLOBAL",0)  //quest is still running  
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",7)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",7)
THEN
	RESPONSE #100
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END


/* dagger was sent back */

IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)	
	GlobalGT("C#Ajantis_CM_SkieCost","GLOBAL",0)
	GlobalLT("C#Ajantis_CM_SkieCost","GLOBAL",5)
	GlobalGT("L#2SDSendIt","GLOBAL",0)
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul 
	!See("L#2SDNEM") 
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",10)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",10)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END

/* after hearing about the cost */

IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)	
	Global("C#Ajantis_CM_SkieCost","GLOBAL",2)
	GlobalGT("L#2SkieDagger","GLOBAL",11)  
	GlobalLT("L#2SkieDagger","GLOBAL",15)  
	Global("L#2SDToldImoenNo","GLOBAL",0) //Player did not leave dagger in ID
	Global("L#2SDFailedLater","GLOBAL",0)  //quest is still running
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul
	!See("L#2SDNEM") 
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",3)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",3)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END


/* dagger was restored with Ajantis in the party */

IF
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#Ajantis_CM_SkieCost","GLOBAL",0)
	GlobalLT("C#Ajantis_CM_SkieCost","GLOBAL",5)
	GlobalGT("L#2SkieDagger","GLOBAL",30) 
	GlobalLT("L#2SkieDagger","GLOBAL",33)
	Global("L#2SDToldImoenNo","GLOBAL",0) //Player did not leave dagger in ID
	Global("L#2SDFailedLater","GLOBAL",0)  //quest is still running 
	Global("L#2SDBreakRule","GLOBAL",0) //PC did not lose Skie's soul
	!See("L#2SDNEM") 
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_CM_SkieCost","GLOBAL",5)
END

/* dialogue triggering */
IF	
	InParty(Myself)
	CombatCounter(0) !See([ENEMY])
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Ajantis_CM_SkieCost","GLOBAL",5)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END





