/* ----------------NPC - Ajantis banters------------------------------------
//NPC banters. One timer, to prevent them of firing all after the same amount of time. */





/* Ajantis greets Keldorn: dialogue activation */
IF
	InParty(Myself)
	InParty("KELDORN") !StateCheck("KELDORN",CD_STATE_NOTVALID)
	Detect("KELDORN")
	!See([ENEMY])
	CombatCounter(0)
	!Range([NEUTRAL],10)
	Global("C#AjantisKeldornWelcome","GLOBAL",0)
	Global("C#AjantisKeldornWelcomeActive","GLOBAL",0)
	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornWelcomeActive","GLOBAL",1)
END

/* Ajantis greets Keldorn: dialogue triggering */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("KELDORN") !StateCheck("KELDORN",CD_STATE_NOTVALID)
	Detect("KELDORN")
	!Range([NEUTRAL],10)
	Global("C#AjantisKeldornWelcomeActive","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		StartDialogueNoSet("KELDORN")
END


/* Ajantis congratulates Sir Anomen: dialogue activation */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("ANOMEN") 
	!StateCheck("ANOMEN",CD_STATE_NOTVALID)
	Detect("ANOMEN")
	!See([ENEMY])
	CombatCounter(0)
	!Range([NEUTRAL],10)
	Global("AnomenIsKnight","GLOBAL",1)
	Global("C#AjantisAnomenCongratulations","GLOBAL",0)
	Global("C#AjantisAnomenCongrats","GLOBAL",0)
	!AreaCheck("AR0900")
	!AreaCheck("AR0903")	
	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
	OR(2)
		Global("C#AjantisPCChoseAnomen","GLOBAL",3)
		Global("C#AjantisPCAnomenRom","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisAnomenCongrats","GLOBAL",1)
END

/* Ajantis congratulates Sir Anomen: dialogue triggering */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("ANOMEN") 
	!StateCheck("ANOMEN",CD_STATE_NOTVALID)
	Detect("ANOMEN")
	Global("C#AjantisAnomenCongrats","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
		StartDialogueNoSet("ANOMEN")
END



//Keldorn, wet clothes

IF //trace if Area was left before the banter was triggered.
	InParty(Myself)
	InParty("keldorn")
	!AreaCheck("AR2600") //Thetyr Woods
	!AreaCheck("AR1900") //Druid grove
	Global("C#AjantisKeldornWetClothes","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornWetClothes","GLOBAL",0)
END 

/* timer setting */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	InMyArea("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	OR(2)
		AreaCheck("AR2600") //Thetyr Woods
		AreaCheck("AR1900") //Druid grove
	Global("C#AjantisKeldornWetClothes","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornWetClothes","GLOBAL",1)
		RealSetGlobalTimer("C#AjantisKeldornTimer","GLOBAL",150) 
END 

/* dialogue activation */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	CombatCounter(0) !See([ENEMY])
	OR(2)
		AreaCheck("AR2600") //Thetyr Woods
		AreaCheck("AR1900") //Druid grove
	Global("C#AjantisKeldornWetClothes","GLOBAL",1)
	RealGlobalTimerExpired("C#AjantisKeldornTimer","GLOBAL")
THEN
	RESPONSE #100
    		SetGlobal("C#AjantisKeldornWetClothes","GLOBAL",2)
END 

/* dialogue triggering */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	Global("C#AjantisKeldornWetClothes","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",200)
		RealSetGlobalTimer("C#AjantisNPCBanterTimer","GLOBAL",2000) //2000
    		StartDialogNoSet("KELDORN")
END 

/* Valygar "looking at the stars" Weather change (no weather) */
IF
  InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
  InParty("valygar")
  RealGlobalTimerExpired("C#AjantisNPCBanterTimer","GLOBAL")
  !Detect([ENEMY])
  Detect("valygar")
  !StateCheck("valygar",CD_STATE_NOTVALID)
  CombatCounter(0)
	!Range([NEUTRAL],10)
  Global("C#AjantisValygarWeather","GLOBAL",0)
  Global("C#AjantisValygarWeatherChange","GLOBAL",0)
  TimeOfDay(NIGHT)
  !AreaType(DUNGEON)
  !AreaType(CITY)
  OR(3)
    AreaType(OUTDOOR)
    AreaType(WEATHER)
    AreaType(FOREST)
THEN
  RESPONSE #100
    SetGlobal("C#AjantisValygarWeatherChange","GLOBAL",1)
    Weather(NOWEATHER)
    RealSetGlobalTimer("C#AjantisValygarWeatherTimer1","GLOBAL",5)
    RealSetGlobalTimer("C#AjantisNPCBanterTimer","GLOBAL",2000) //2000
    MoveToObject("valygar")
END

/* Valygar "looking at the stars": dialogue activation */
IF
  InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
  InParty("valygar")
  Detect("valygar")
  !StateCheck("valygar",CD_STATE_NOTVALID)
  !Detect([ENEMY])
  CombatCounter(0)
	!Range([NEUTRAL],10)
  Global("C#AjantisValygarWeather","GLOBAL",0)
  TimeOfDay(NIGHT)
  !AreaType(DUNGEON)
  !AreaType(CITY)
  OR(3)
    AreaType(OUTDOOR)
    AreaType(WEATHER)
    AreaType(FOREST)
  Global("C#AjantisValygarWeatherChange","GLOBAL",1)
  RealGlobalTimerExpired("C#AjantisValygarWeatherTimer1","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("C#AjantisValygarWeather","GLOBAL",1)
END

/* Valygar "looking at the stars": dialogue triggering */
IF
  InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
  InParty("valygar")
  Detect("valygar")
  !StateCheck("valygar",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
  Global("C#AjantisValygarWeather","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet("valygar")
END


/* Greets Imoen after Spellhold: dialogue activation */
IF
  InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
  GlobalGT("Chapter","GLOBAL",%bg2_chapter_3%)
  !Global("C#IM_ImoenStays","GLOBAL",1)
  InParty("imoen2")
  Detect("imoen2") 
  CombatCounter(0)
  !See([ENEMY])
	!Range([NEUTRAL],10)
  !StateCheck("imoen2",CD_STATE_NOTVALID)
  !AreaType(DUNGEON)
  Global("C#Ajantis_ImoenSpellholdBanter","GLOBAL",0)
  RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("C#Ajantis_ImoenSpellholdBanter","GLOBAL",1)
END

/* Greets Imoen after Spellhold: dialogue triggering */
IF
  InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
  InParty("imoen2")
  Detect("imoen2") 
	!Range([NEUTRAL],10)
  !StateCheck("imoen2",CD_STATE_NOTVALID)
  Global("C#Ajantis_ImoenSpellholdBanter","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#AjantisNPCBanterTimer","GLOBAL",2000) 
    RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600)
    StartDialogNoSet("imoen2")
END


/* Haer'Dalis banter for tired elf (no timer): dialogue activation */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("haerdalis")
	Detect("haerdalis")
	InMyArea(Player1)
	!StateCheck("haerdalis",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	CombatCounter(0) !See([ENEMY])
	CheckStatGT("haerdalis",6,FATIGUE) //
	Global("C#AjantisHaerDalis1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisHaerDalis1","GLOBAL",1)
END

/* Haer'Dalis banter for tired elf (no timer): dialogue triggering */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("haerdalis")
	Detect("haerdalis")
	!StateCheck("haerdalis",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	Global("C#AjantisHaerDalis1","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogNoSet("haerdalis")
END

//Haer'Dalis talk in an inn (talk triggered via c#AjanD.bcs)

//Minsc banter at rest, outdoors (talk triggered via c#AjanD.bcs)

//Timer for Haer'Dalis Aerie romance - Aerie banter
IF
	InParty(Myself)
	InParty("haerdalis")
	InParty("aerie")
	Global("C#AjantisHaerDalAerie1","GLOBAL",0)
	Global("HaerDalisRomanceActive","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisHaerDalAerie1","GLOBAL",1)
		RealSetGlobalTimer("C#AjantisHaerDalAerie1Timer","GLOBAL",1200) //1200
END


/* banter with Aerie concerning Haer'Dalis' romance: dialogue activation */
IF
   InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
   InParty("haerdalis")
   InParty("aerie")
   !Dead("aerie")
   !Dead("haerdalis")
   Detect("aerie")
   InMyArea(Player1)
   !StateCheck("aerie",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	CombatCounter(0) !See([ENEMY])
   !Global("HaerDalisRomanceActive","GLOBAL",3)
   Global("C#AjantisHaerDalAerie1","GLOBAL",1)
   RealGlobalTimerExpired("C#AjantisHaerDalAerie1Timer","GLOBAL")
THEN
   RESPONSE #100
      SetGlobal("C#AjantisHaerDalAerie1","GLOBAL",2)
END

/* banter with Aerie concerning Haer'Dalis' romance: dialogue triggering */
IF
   InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
   InParty("aerie")
   InMyArea("aerie")
   !StateCheck("aerie",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
   Global("C#AjantisHaerDalAerie1","GLOBAL",2)
THEN
   RESPONSE #100
      StartDialogNoSet("aerie")
END

//Timer for Haer'Dalis Aerie romance - HD banter
IF
	InParty(Myself)
	InParty("haerdalis")
	InParty("aerie")
	Global("C#AjantisHaerDalAerie","GLOBAL",0)
	Global("HaerDalisRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisHaerDalAerie","GLOBAL",1)
		RealSetGlobalTimer("C#AjantisHaerDalAerieTimer","GLOBAL",600) //
END

/* banter: dialogue activation */
IF
   InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
   InParty("haerdalis")
   InParty("aerie")
   Detect("haerdalis")
   !Dead("aerie")
   !Dead("haerdalis")
   InMyArea(Player1)
   !StateCheck("haerdalis",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	CombatCounter(0) !See([ENEMY])
   Global("HaerDalisRomanceActive","GLOBAL",2)
   Global("C#AjantisHaerDalAerie","GLOBAL",1)
   RealGlobalTimerExpired("C#AjantisHaerDalAerieTimer","GLOBAL")
THEN
   RESPONSE #100
      SetGlobal("C#AjantisHaerDalAerie","GLOBAL",2)
END

/* banter: dialogue triggering */
IF
   InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
   InParty("haerdalis")
   Detect("haerdalis")
   !StateCheck("haerdalis",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
   Global("C#AjantisHaerDalAerie","GLOBAL",2)
THEN
   RESPONSE #100
      StartDialogNoSet("haerdalis")
END



//Keldorn's quest

/*
"C#AjantisKeldornMaria"

1: timer set

2: banter "Keldorn forgave Maria" triggered (Global("LadyMaria","GLOBAL",5))

3: banter run and triggers next Ajantis-Keldorn banter about obiding law

4: triggers "Keldorn put Maria in prison" banter

5: banters run, and triggers last banter

6: last banter run
*/

IF
  InParty(Myself)  
  InParty("keldorn")
	OR(2)
		Global("LadyMaria","GLOBAL",4)
		Global("LadyMaria","GLOBAL",5)
	Global("C#AjantisKeldornMaria","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornMaria","GLOBAL",1)
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",600) //1200
END 

/* Global("LadyMaria","GLOBAL",5): Keldorn forgave Maria: dialogue activation */

IF
	InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Detect([ENEMY])
	CombatCounter(0)
	!Range([NEUTRAL],10)
	Global("LadyMaria","GLOBAL",5)
	Global("C#AjantisKeldornMaria","GLOBAL",1)
  	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
	GlobalLT("B!KeldornRomReturn","GLOBAL",5) //not for b's Keldorn romance mod
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornMaria","GLOBAL",2)
END 

/* Global("LadyMaria","GLOBAL",4): Keldorn brought Maria to prison: dialogue activation */

IF
	InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Detect([ENEMY])
	CombatCounter(0)
	!Range([NEUTRAL],10)
	Global("LadyMaria","GLOBAL",4)
	Global("C#AjantisKeldornMaria","GLOBAL",1)
  	RealGlobalTimerExpired("C#AjantisDialogPausenTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornMaria","GLOBAL",4)
END 

/* dialogues triggering */

IF
	InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	OR(2)
		Global("C#AjantisKeldornMaria","GLOBAL",2)
		Global("C#AjantisKeldornMaria","GLOBAL",4)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjantisDialogPausenTimer","GLOBAL",100)
		RealSetGlobalTimer("C#AjantisNPCBanterTimer","GLOBAL",2000) //2000
    		StartDialogNoSet("KELDORN")
END 

/* b's Keldorn Romance: only last dialogue triggers */

IF
	InParty(Myself)  
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("keldorn")
	Detect("keldorn")
	!StateCheck("keldorn",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	GlobalLT("C#AjantisKeldornMaria","GLOBAL",5)
	GlobalGT("B!KeldornRomReturn","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisKeldornMaria","GLOBAL",5) //enables banter
END 


/* Ajantis-Anomen "sword banter" in the Adventurers Mart */

/* Ajantis-Anomen "sword banter": dialogue activation */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("anomen")
	Detect("anomen")	
	!StateCheck("anomen",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	CombatCounter(0) !See([ENEMY])
	OR(2)
		Global("AnomenIsNotKnight","GLOBAL",1)
		Global("AnomenIsKnight","GLOBAL",1)
	AreaCheck("AR0702")
	Global("C#AjantisAnomenSwordBanter","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisAnomenSwordBanter","GLOBAL",1)
END

/* Ajantis-Anomen "sword banter": dialogue triggering */
IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty("anomen")
	Detect("anomen")	
	!StateCheck("anomen",CD_STATE_NOTVALID)
	!Range([NEUTRAL],10)
	Global("C#AjantisAnomenSwordBanter","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogNoSet("anomen")
END
